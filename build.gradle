buildscript {
    project.ext['artifactoryDomainName'] = 'https://artifactory.viralgains.com/artifactory'
    project.ext['artifactoryUsername'] = 'jenkins-development'
    project.ext['artifactoryPassword'] = 'eURsomicKWAtione'

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies {
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:2.0.0-beta1"
        classpath 'com.bmuschko:gradle-clover-plugin:2.2.1'
        classpath 'gradle.plugin.com.github.jk1:gradle-license-report:1.3'
    }
}

apply plugin: 'java'
apply plugin: "com.gorylenko.gradle-git-properties" //generates git.properties, which is then visible at /application/info
//MAL: disabling eclipse plugin unless someone actually needs it
//apply plugin: 'eclipse'
apply plugin: 'com.bmuschko.clover'
apply plugin: 'com.github.jk1.dependency-license-report'

group 'com.mediamath'
version = '1.2.1'
sourceCompatibility = 1.8

//don't repackage the jar as an executable, this is just a simple library jar
jar.enabled = true

repositories {
    mavenCentral()
    maven {
        credentials {
            username "$artifactoryUsername"
            password "$artifactoryPassword"
        }
        url "$artifactoryDomainName/viralgains-libs"
    }
}

//for dynamically changing modules, such as SNAPSHOT builds, have gradle recheck each time (needed for our odyssey-common)
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}

//For non-spring dependencies, specific versions need to be set to ensure that the same version is used by everyone.
dependencies {
    compile('junit:junit:4.12')
    compile('org.glassfish.jersey.core:jersey-client:2.25.1')
    compile('org.glassfish.jersey.media:jersey-media-multipart:2.25.1')
    compile('org.slf4j:slf4j-api:1.7.25')
    compile('org.slf4j:slf4j-log4j12:1.7.25')
    compile('com.google.code.gson:gson:2.8.1')
    compile('org.mockito:mockito-core:2.8.47')
    compile('org.mockito:mockito-all:1.10.19')
    compile('com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.2')
    compile('org.apache.oltu.oauth2:org.apache.oltu.oauth2.client:1.0.2')
    compile('org.apache.oltu.oauth2:org.apache.oltu.oauth2.common:1.0.2')
    clover 'org.openclover:clover:4.2.0'
}

gitProperties {
    customProperty 'git.vg_build_number', "${System.env.tagPrefix ?: ''}${System.env.BUILD_NUMBER ?:''}" //gets tagPrefix and BUILD_NUMBER from Jenkins build environment variable and puts into git properties so /info endpoint picks it up
    keys = ['git.vg_build_number','git.build.time','git.branch','git.commit.id','git.commit.id.abbrev','git.commit.message.full','git.commit.time','git.commit.user.name'] //configures which keys to put
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
}
//Functional tests can be enabled when we build environment for real tests
test {
    exclude '**/functional/**'
}

clover{
    compiler {
        // used to add debug information for Spring applications. This allows the @PathVariable 'value' element to be optional.
        debug = true
    }
    contexts {
        statement {
            name = 'log'
            regexp = '^.*LOG\\..*'
        }

        method {
            name = 'main'
            regexp = 'public static void main\\(String args\\[\\]\\).*'
        }
        method {
            name = 'getters'
            regexp = 'public [^\\s]+ get[A-Z][^\\s]+\\(\\)'
            maxStatements = 1
        }
        method {
            name = 'setters'
            regexp = 'public void set[A-Z][^\\s]+\\(.+\\)'
            maxStatements = 1
        }
    }
    report{
        html = true
        filter = 'log,main,getters,setters'
    }
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/main/generated']
        }
    }
}

import com.github.jk1.license.render.*
licenseReport {
    renderers = [new CsvReportRenderer("licenses-${project.name}.csv")] //render dependencies as csv, one dependency per line
}
